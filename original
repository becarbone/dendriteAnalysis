//Redone - Bea C. 2016

// ImageJ initialize
run("Colors...", "foreground=white background=black selection=yellow");
run("Options...", "iterations=1 count=1 black edm=Overwrite");
run("Set Measurements...", "area perimeter area_fraction limit redirect=None decimal=3");

//set raw result folder.   rawdir contains multiple subfolders, each subfolder contains multiple raw images
// each image is RGB format
// result folder should be empty.
rawdir=getDirectory("Choose Raw Data Folder");
resultdir=getDirectory("Choose Result Folder");
axondir=getDirectory("Choose AxonShow Folder");

//variable setup
var greenThreshold, redThreshold, areapercent, scale, minsize, maxsize, name, gcount, rcount, grcount, rgcount, axonlength, axonflag
var greenstring, redstring, resultlist, FolderCreate, foldername;
// input parameter
parameterinput();

print("Raw_Folder:	"+rawdir);
print("Result_Folder:	"+resultdir);
print("GreenThreshold	RedThreshold	AreaPercent(%)	PixelScale(um/pixel)	Min_Puncta_Size	Max_Puncta_Size");
print(greenThreshold+"	"+redThreshold+"	"+areapercent+"	"+scale+"	"+minsize+"	"+maxsize);
print(" 	GreenPuncta#	RedPuncta#	GR#	RG#	AxonLength(um)	Density_G(#/um)	Density_R(#/um)");

//OK! so I think what's happening here is the mkdir command is broken and so there's no path for it to find 
//the new folder - need to make a path.


folderlist=getFileList(rawdir);

Dialog.create("Conditions");
	Dialog.addMessage("How many conditions do you have?");
	conditions = newArray(1,2,3,4,5);
	Dialog.setInsets(2,40,2);
	Dialog.addChoice("",conditions);
Dialog.show();
FolderCreate = Dialog.getChoice();

for(f=0;f<FolderCreate;f++) 
	{
	greenstring="Filename	#	Area	Perim	%Area\n";
	redstring="Filename	#	Area	Perim	%Area\n";
	foldername = File.getName(rawdir+folderlist[f]);
	File.makeDirectory(resultdir+foldername);
	folderpath = getDirectory("Choose folder for condition " + (f+1)); 
	rawlist=getFileList(rawdir+foldername);
	filelist=getFileList(rawdir+folderlist[f]);
	resultpath = getFileList(folderpath);
	print(folderlist[f]);
	for(d=0;d<filelist.length;d++)
		{
		// initialize :  open file, split channel, name by "red"/"green"/"axon"
		initialize();
		// create green red mask images and roi files
		mask("green"); 
		mask("red");
		// mask axon, and get length;
		axon();
		//measure
		measure();
		temptitle = File.getName(folderpath);
		}
	File.saveString(greenstring, folderpath+temptitle+"_GreenArea.xls");
	File.saveString(redstring, folderpath+temptitle+"_RedArea.xls");  
	}
selectWindow("Log");
saveAs("Text", resultdir+"Summary.xls");
selectWindow("Log"); run("Close");
selectWindow("Results"); run("Close");
selectWindow("ROI Manager"); run("Close");

function parameterinput()
	{
	Dialog.create("Parameter");
		Dialog.addNumber("Green Threshold:", 30);
		Dialog.addNumber("Red Threshold:", 30);
		Dialog.addNumber("Area Percent (%):", 25);
		Dialog.addNumber("Pixel Scale (um/pixel):", 0.1705);
		Dialog.addNumber("Min Punta Size (pixel):", 1);
		Dialog.addNumber("Max Punta Size (pixel):", 60);
	Dialog.show();
	greenThreshold = Dialog.getNumber();
	redThreshold = Dialog.getNumber();
	areapercent = Dialog.getNumber();  
	scale = Dialog.getNumber();  
	minsize = Dialog.getNumber();  
	maxsize = Dialog.getNumber();  
	}

function initialize()
 {
	open(rawdir+foldername+File.separator+rawlist[d]);
	name=getInfo("image.filename");
	run("Properties...", "channels=1 slices=1 frames=1 unit=um pixel_width="+scale+" pixel_height=0.1705 voxel_depth=1");
	run("Split Channels");
	rename("axon");
	selectImage(name+" (green)"); rename("green");
	selectImage(name+" (red)"); rename("red");
 }

function mask(channel)
	{
	selectImage(channel);
	run("Gaussian Blur...", "sigma=1");
	if( channel == "green") {
		setThreshold(greenThreshold, 255);
		}
	if( channel == "red") {
		setThreshold(redThreshold, 255);
		}
	run("Find Maxima...", "noise=10 output=[Segmented Particles] above");
	run("Analyze Particles...", "size="+minsize+"-"+maxsize+" pixel show=Masks add");
	selectWindow("Mask of "+channel+" Segmented");  
	run("Grays");
	saveAs("Tiff", folderpath+name+"_"+channel+"_mask.tif");
	run("Clear Results");
	roiManager("Measure");
	selectWindow("Results");
  
	if( channel == "green")
		{
		for(j=0;j<nResults;j++) {
			greenstring = greenstring+filelist[d]+"	"+j+1+"	"+getResult("Area",j)+"	"+getResult("Perim.",j)+" 	"+getResult("%Area",j)+"\n";
			}
		}
	if( channel == "red")
		{
		for(j=0;j<nResults;j++) {
			redstring = redstring+filelist[d]+"	"+j+1+"	"+getResult("Area",j)+"	"+getResult("Perim.",j)+" 	"+getResult("%Area",j)+"\n";
			}
		}
  
	run("Clear Results");
	rename(channel+" mask");
	selectImage(channel); close();
	selectImage(channel+" Segmented"); close();

	if( channel == "green" )  {
		gcount = roiManager("Count");
		}
	if( channel == "red" )  {
		rcount = roiManager("Count");
		}
  
	if( roiManager("Count") > 0 )
		{
		roiManager("Save",folderpath+name+"_"+channel+"_ROI.zip");
		}
	roiManager("reset");
	}

function axon()
	{
	selectImage("axon");
	run("Subtract Background...", "rolling=10");
	run("Gaussian Blur...", "sigma=3");
	setAutoThreshold("Triangle dark");
	setOption("BlackBackground", true);
	run("Convert to Mask");
	run("Skeletonize");
	run("Analyze Particles...", "size=25-infinity pixel show=Nothing add");
  
	axonlength=0;
	if( roiManager("Count") > 0 )
		{
		roiManager("Save", folderpath+name+"_axon.zip");
		run("Clear Results");
		roiManager("Measure");

		for(i=0; i< roiManager("Count"); i++)
		axonlength= axonlength + getResult("Perim.",0) ;
		axonlength = axonlength/2;

		open(rawdir+foldername+File.separator+rawlist[d]);
		roiManager("Show All");
		run("Flatten");
    	saveAs("Jpeg", axondir+foldername+"_"+name+"_show.jpg");
		close(); close();
		}
	roiManager("reset"); 
	run("Clear Results");
	}
function measure()
	{
	if( gcount > 0 ) {
  		roiManager("Open", folderpath+name+"_green_ROI.zip");
		}
	selectImage("red mask");
	run("Clear Results");
	roiManager("Measure");
	grcount=0;
	for(i=0;i<roiManager("Count");i++) {
		if( getResult("%Area",i) >= areapercent ) {
			grcount++;
			}
		}
	roiManager("reset"); 
	run("Clear Results");
	
	if( rcount > 0 ) {
  		roiManager("Open", folderpath+name+"_red_ROI.zip");
		}
	selectImage("green mask");
	run("Clear Results");
	roiManager("Measure");
	rgcount=0;
	for(i=0;i<roiManager("Count");i++) {
		if( getResult("%Area",i) >= areapercent ) {
			rgcount++;
			}
		}
	roiManager("reset"); 
	run("Clear Results");
	
	//print(" 	GreenPuncta#	RedPuncta#	GR#	RG#	AxonLength(um)	Density_G(um/#)	Density_R(um/#)");
	print(name+"	"+gcount+"	"+rcount+"	"+grcount+"	"+rgcount+"	"+axonlength+"	"+(gcount/axonlength)+"	"+(rcount/axonlength));
 
	run("Close All"); 
	}



